# load button proxies click to hidden file input
document.getElementById \load .addEventListener \click -> 
	document.getElementById \file .click!

document.getElementById \file .addEventListener \change !->
	reader = new FileReader
	builtins = <[Array.vm Keyboard.vm Math.vm Memory.vm Output.vm Screen.vm String.vm Sys.vm]>

	start = document.getElementById \start
	start.disabled = true
	start.textContent = "translating.."

	# gotta do some fancy async code in order to translate
	# all the needed files
	files = Array::filter.call @files, -> 
		# return all non-builtin .vm files
		/^.+\.vm$/.test it.name and (builtins.indexOf it.name) is -1
	
	file_texts = []

	# this processes them in serial, using only one filereader
	# using multiple readers for parallel process is probably
	# of dubious value
	reader.onload = ->
		file_texts.push @result
		if files.length is 0
			try
				translate file_texts
				vm.reset!
				start.click! # start the game
			catch e
				alert "Error translating: #e"
				start.textContent = "Start"
				start.disabled = false
		else
			@readAsText files.pop!
	
	if files.length > 0
		reader.readAsText files.pop!
	else
		alert "No non-OS .vm files found in your selection!"

document.getElementById \start .addEventListener \click !-> vm.start!
document.getElementById \stop .addEventListener \click !-> vm.stop!
document.getElementById \reset .addEventListener \click !-> vm.reset!

# set computer's screen context to canvas element
vm.canvas = document.getElementById \canvas .getContext \2d

# listen on document for key events
document.addEventListener \keydown vm.keyboard
document.addEventListener \keyup vm.clearkeyboard

# computer's references
vm.startbutton = document.getElementById \start
vm.stopbutton = document.getElementById \stop
vm.resetbutton = document.getElementById \reset

# disable buttons to begin
vm.startbutton.disabled = true
vm.stopbutton.disabled = true
vm.resetbutton.disabled = true

# alert runtime errors
window.addEventListener \error ->
	alert "Runtime error! Current callstack: #{vm.callstack?map( -> it.fn_name ).join ', '}"
